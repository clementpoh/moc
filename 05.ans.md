
# Week 5 question 2:

The six list comprehensions, together with some warnings/pitfalls:

(1) is best done by making use of "conformant pattern matching" -
    we say that we only want those pairs from act that are instances
    of the pattern (Kevin Spacey", _):

    ```
    -> [film | ("Kevin Spacey", film) <- act]
    ["Seven","The Usual Suspects"]
    ```

(2) is easy enough; a generator, followed by a predicate:

    ```
    -> [x | x <- actors, isDirector x]
    ["Quentin Tarantino"]
    ```

(3) shows that we can easily generate pairs:

    ```
    -> [(actor,film) | (actor,film) <- act, isDirector actor]
    [("Quentin Tarantino","Pulp Fiction"),("Quentin Tarantino","Reservoir Dogs")]
    ```

(4) This one is tricky because year is a string, not a number;
    the easiest way to convert the string is with read:

    ```
    -> [film | (film,year) <- release, (read year :: Int) > 2000]
    ["The Bourne Identity"]
    ```

(5) We can't have two generators - for one of them we use the predicate:

    ```
    -> [film | ("Harvey Keitel",film) <- act, doesAct ("John Travolta",film)]
    ["Pulp Fiction"]
    ```

(6) Here is would be tempting to use `(dir,film) <- direct, (film,date) <-
    release` as the generators; but Haskell does not allow that; hence:

    ```
    -> [(dir,film,date) | (dir,film) <- direct, (x,date) <- release, x==film]
    [("Brian De Palma","The Untouchables","1987"),...]
    ```


## Week 5 question 4:

Help them along, but don't give too much away - let them work for it.
They will need this as part of Asg 2, so they should own the solution.

```haskell
mkXNF :: [Monomial] -> [Monomial]
mkXNF
  = remove_Pairs . sort            -- or just use nub

remove_Pairs :: Eq a => [a] -> [a]
remove_Pairs []
  = []
remove_Pairs [x]
  = [x]
remove_Pairs (x:y:xs)
  | x == y = remove_Pairs xs
  | otherwise = x : remove_Pairs (y:xs)

myFalse :: Formula
myFalse
  = []

myTrue :: Formula
myTrue
  = [[]]

myLiteral :: String -> Formula
myLiteral s
  = [[s]]

myNot :: Formula -> Formula
myNot f
  | [] `elem` f = delete [] f
  | otherwise   = mkXNF ([] : f)

myAnd :: Formula -> Formula -> Formula
myAnd f1 f2
  = mkXNF [mkMonomial (m1 ++ m2) | m1 <- f1, m2 <- f2]

myOr :: Formula -> Formula -> Formula
myOr f1 f2
  = mkXNF (f1 ++ f2 ++ [mkMonomial (m1 ++ m2) | m1 <- f1, m2 <- f2])

myNand :: Formula -> Formula -> Formula
myNand f1 f2
  = mkXNF (myTrue ++ [mkMonomial (m1 ++ m2) | m1 <- f1, m2 <- f2])

myNor :: Formula -> Formula -> Formula
myNor f1 f2
  = mkXNF (myTrue ++ f1 ++ f2 ++ [mkMonomial (m1 ++ m2) | m1 <- f1, m2 <- f2])

myXor :: Formula -> Formula -> Formula
myXor f1 f2
  = mkXNF (f1 ++ f2)

myBiim :: Formula -> Formula -> Formula
myBiim f1 f2
  = mkXNF (myTrue ++ f1 ++ f2)
```
